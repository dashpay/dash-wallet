apply plugin: 'com.android.application'
apply plugin: 'project-report'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

ext {
    archivesBaseName = 'dash-wallet'
}
repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
}

dependencies {
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation(project(':integration-android')) {
        exclude(group: 'com.google.android', module: 'android')
    }
    //Liquid Integration
    implementation project(path: ':liquid-integration')

    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation "androidx.fragment:fragment-ktx:1.3.6"
    implementation('androidx.legacy:legacy-support-v13:1.0.0') {
        exclude(group: 'com.android.support', module: 'support-fragment')
        exclude(group: 'com.android.support', module: 'support-media-compat')
    }
    implementation 'androidx.legacy:legacy-support-core-utils:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android:flexbox:1.1.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.0'
    implementation "androidx.work:work-runtime-ktx:2.4.0"
    implementation "org.dashj:dashj-core:$dashjVersion"
    implementation 'org.dashj.platform:dashpay:0.21-SNAPSHOT'
    implementation 'org.dashj.platform:platform-core:0.21-SNAPSHOT'
    implementation 'org.dashj.platform:dpp:0.21-SNAPSHOT'
    implementation 'org.dashj.platform:dapi-client:0.21-SNAPSHOT'
    implementation 'org.dashj:dashj-merk:0.20-SNAPSHOT'
    implementation 'org.dashj.android:dashj-merk:0.20-SNAPSHOT'
    implementation 'io.grpc:grpc-stub:1.28.0' // CURRENT_GRPC_VERSION
    implementation "org.dashj.android:dashj-bls-android:0.18"
    implementation "org.dashj.android:dashj-x11-android:0.17.5"
    implementation "org.dashj.android:dashj-scrypt-android:0.17.5"
    implementation 'io.grpc:grpc-stub:1.28.0' // CURRENT_GRPC_VERSION
    implementation 'com.google.guava:guava:29.0-android'
    implementation 'com.google.zxing:core:3.3.3' // 3.3.3 is the maximum to support Android 4.4 to 6.x

    implementation "com.squareup.okhttp3:okhttp:$ok_http_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$ok_http_version"
    implementation 'com.squareup.moshi:moshi:1.11.0'
    implementation 'com.squareup.moshi:moshi-kotlin:1.11.0'
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation('com.github.tony19:logback-android-classic:1.1.1-6') {
        exclude(group: 'com.google.android', module: 'android')
    }
    implementation 'com.github.tony19:logback-android-core:1.1.1-6'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.tbuonomo.andrui:viewpagerdotsindicator:4.1.2'
    implementation 'com.sambarboza:orbitview:0.2.0'
    implementation 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'

    implementation project(path: ':common')
    implementation 'com.google.android.gms:play-services-auth:19.0.0'

    // Database
    implementation "androidx.room:room-ktx:$roomVersion"
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // Uphold Integration
    implementation project(path: ':uphold-integration')

    implementation 'androidx.multidex:multidex:2.0.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1"

    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"

    implementation 'com.github.MikeOrtiz:TouchImageView:3.0.3'

    // Declare the dependencies for the Crashlytics and Analytics libraries
    // When using the BoM, you don't specify versions in Firebase library dependencies
    implementation platform("com.google.firebase:firebase-bom:$firebaseVersion")
    implementation 'com.google.firebase:firebase-inappmessaging-display'
    implementation 'com.google.firebase:firebase-dynamic-links'

    // Declare the dependency for the Performance Monitoring library
    // When using the BoM, you don't specify versions in Firebase library dependencies
    //implementation 'com.google.firebase:firebase-perf-ktx'

    // Google Drive Dependencies
    implementation ('com.google.api-client:google-api-client-android:1.26.0') {
        exclude group: 'com.google.guava', module: 'guava-jdk5'
    }
    implementation('com.google.apis:google-api-services-drive:v3-rev173-1.25.0') {
        exclude group: 'com.google.guava', module: 'guava-jdk5'
    }

    implementation ('com.google.http-client:google-http-client-gson:1.26.0') { // do we need this
        exclude group: 'com.google.guava', module: 'guava-jdk5'
    }

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"

    implementation 'com.jakewharton:process-phoenix:2.0.0'

    // DI
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-compiler:$hiltVersion"
    implementation 'androidx.hilt:hilt-work:1.0.0'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'

    // Tests
    testImplementation 'junit:junit:4.13.2'
    //Espresso
    androidTestImplementation('androidx.test:runner:1.3.0') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation('androidx.test.espresso:espresso-core:3.3.0') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation('androidx.test.espresso:espresso-intents:3.3.0') {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    // DI for instrumentation tests
    androidTestImplementation  "com.google.dagger:hilt-android-testing:$hiltVersion"
    kaptAndroidTest "com.google.dagger:hilt-compiler:$hiltVersion"

    // DI for local unit tests
    testImplementation "com.google.dagger:hilt-android-testing:$hiltVersion"
    kaptTest "com.google.dagger:hilt-compiler:$hiltVersion"
}

kapt {
    correctErrorTypes true
}

hilt {
    enableAggregatingTask = true
}

ext {
    archivesBaseName = 'dash-wallet'
}

def props = new Properties()
if (file("../service.properties").exists()) {
    file("../service.properties").withInputStream { props.load(it) }
}

android {
    compileSdkVersion 30
    android.ndkVersion '21.3.6528147'

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode project.hasProperty('versionCode') ? project.property('versionCode') as int : 80109
        versionName project.hasProperty('versionName') ? project.property('versionName') : "5.0-dashpay"
        multiDexEnabled true
        generatedDensities = ['hdpi', 'xhdpi']
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
        buildConfigField("String", "UPHOLD_CLIENT_ID", "\"UPHOLD_CLIENT_ID\"")
        buildConfigField("String", "UPHOLD_CLIENT_SECRET", "\"UPHOLD_CLIENT_SECRET\"")
        buildConfigField("String", "LIQUID_PUBLIC_API_KEY", "\"LIQUID_PUBLIC_API_KEY\"")
    }

    signingConfigs {
        debug {
            storeFile file('keystore/debug.keystore')
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        all {
            def imgurClientId = props.getProperty("IMGUR_CLIENT_ID")
            def imgurClientSecret = props.getProperty("IMGUR_CLIENT_SECRET")
            if (imgurClientId == null) {
                imgurClientId = "\"IMGUR_CLIENT_ID\""
            }
            if (imgurClientSecret == null) {
                imgurClientSecret = "\"IMGUR_CLIENT_SECRET\""
            }
            buildConfigField("String", "IMGUR_CLIENT_ID", imgurClientId)
            buildConfigField("String", "IMGUR_CLIENT_SECRET", imgurClientSecret)
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg', 'proguardTest-rules.txt'
        }
        debug {
            multiDexEnabled true
            debuggable true
        }
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        test {
            java.srcDirs = ['test']
            resources.srcDirs = ['test']
        }
        androidTest {
            java.srcDirs = ['androidTest']
            resources.srcDirs = ['androidTest/java']
            res.srcDirs = ['androidTest/testNet3/res']
        }
        _testNet3 {
            res.srcDirs = ["testNet3/res"]
        }
        staging {
            res.srcDirs = ["staging/res"]
        }
        schnapps {
            res.srcDirs = ["schnapps/res"]
        }
    }
    flavorDimensions "default"
    productFlavors {
        prod {
            applicationId = "hashengineering.darkcoin.wallet"
            def upholdClientId = props.getProperty("UPHOLD_CLIENT_ID")
            def upholdClientSecret = props.getProperty("UPHOLD_CLIENT_SECRET")
            def liquidClientId = props.getProperty("LIQUID_PUBLIC_API_KEY")
            if (upholdClientId == null) {
                upholdClientId = "\"UPHOLD_CLIENT_ID\""
            }
            if (upholdClientSecret == null) {
                upholdClientSecret = "\"UPHOLD_CLIENT_SECRET\""
            }
            if (liquidClientId == null) {
                liquidClientId = "\"LIQUID_PUBLIC_API_KEY\""
            }
            buildConfigField("String", "UPHOLD_CLIENT_ID", upholdClientId)
            buildConfigField("String", "UPHOLD_CLIENT_SECRET", upholdClientSecret)
            buildConfigField("String", "LIQUID_PUBLIC_API_KEY", liquidClientId)
        }
        _testNet3 {
            applicationId = "hashengineering.darkcoin.wallet_test"
            def imgurClientId = props.getProperty("UPHOLD_CLIENT_ID_SANDBOX")
            def imgurClientSecret = props.getProperty("UPHOLD_CLIENT_SECRET_SANDBOX")
            def liquidClientId = props.getProperty("LIQUID_PUBLIC_API_KEY_SANDBOX")
            if (imgurClientId == null) {
                imgurClientId = "\"UPHOLD_CLIENT_ID\""
            }
            if (imgurClientSecret == null) {
                imgurClientSecret = "\"UPHOLD_CLIENT_SECRET\""
            }
            if (liquidClientId == null) {
                liquidClientId = "\"LIQUID_PUBLIC_API_KEY\""
            }
            buildConfigField("String", "UPHOLD_CLIENT_ID", imgurClientId)
            buildConfigField("String", "UPHOLD_CLIENT_SECRET", imgurClientSecret)
            buildConfigField("String", "LIQUID_PUBLIC_API_KEY", liquidClientId)
        }
        staging {
            applicationId = "org.dash.dashpay.testnet"
            def imgurClientId = props.getProperty("UPHOLD_CLIENT_ID_SANDBOX")
            def imgurClientSecret = props.getProperty("UPHOLD_CLIENT_SECRET_SANDBOX")
            def liquidClientId = props.getProperty("LIQUID_PUBLIC_API_KEY_SANDBOX")
            if (imgurClientId == null) {
                imgurClientId = "\"UPHOLD_CLIENT_ID\""
            }
            if (imgurClientSecret == null) {
                imgurClientSecret = "\"UPHOLD_CLIENT_SECRET\""
            }
            if (liquidClientId == null) {
                liquidClientId = "\"LIQUID_PUBLIC_API_KEY\""
            }
            buildConfigField("String", "UPHOLD_CLIENT_ID", imgurClientId)
            buildConfigField("String", "UPHOLD_CLIENT_SECRET", imgurClientSecret)
            buildConfigField("String", "LIQUID_PUBLIC_API_KEY", liquidClientId)
        }
        schnapps {
            applicationId = "org.dash.dashpay.schnapps"
            buildConfigField("String", "UPHOLD_CLIENT_ID", "\"UPHOLD_CLIENT_ID\"")
            buildConfigField("String", "UPHOLD_CLIENT_SECRET", "\"UPHOLD_CLIENT_SECRET\"")
            buildConfigField("String", "LIQUID_PUBLIC_API_KEY", "\"LIQUID_PUBLIC_API_KEY\"")
        }
    }

    packagingOptions {
        exclude 'lib/x86_64/darwin/libscrypt.dylib'
        exclude 'lib/x86_64/freebsd/libscrypt.so'
        exclude 'lib/x86_64/linux/libscrypt.so'
        exclude 'com/google/thirdparty/publicsuffix/PublicSuffixPatterns.gwt.xml'
        exclude 'com/google/thirdparty/publicsuffix/PublicSuffixType.gwt.xml'
        exclude 'org/bitcoinj/crypto/mnemonic/wordlist/english.txt'
        exclude 'org/bitcoinj/crypto/cacerts'
        exclude 'org.bitcoin.production.checkpoints.txt'
        exclude 'org.bitcoin.test.checkpoints.txt'
        exclude 'google/protobuf/*.proto'
        exclude 'META-INF/DEPENDENCIES'
    }
    dexOptions {
        preDexLibraries true
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
}

apply from: file("../gradle/google-services.gradle")